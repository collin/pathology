# Pathology

### The scientific study of the nature of ~~disease~~ JavaScript and its causes, processes, development, and consequences.

The disturbing truth about CoffeeScript is that it lets you completely obliterate JavaScript
and create your own "language" out of the pieces without ending up with a syntax that makes
you claw your eyes out.

```coffee
puts = console.log
```

## Classes
Pathology ships with a core class: "Pathology.Object".

### Object#def (Object, slots)
Adds each key and value of slots object to the class prototype.
Any Function values are specially juiced to allow calls to super.

```coffee
Object.def key: "value"
Object.def method: -> console.log @key
puts Object.protottype
# => {
  ...
  key: "value"
  method: [Fuction (juiced to allow calls to super) function() {
    return console.log(this.key);
  }]
}
```

### Object#defs ([Object] slots)
Adds each key and value of slots object to the class directly.

```coffee
Object.defs key: "value"
puts Object.key
# => "value"
```

### Object#extend ([Function] body)
### Object#open ([Function] body)
### Object#include ([Pathology.Module] ...modules)
### Object#new (...arguments)

### Pretty toString on objects.

```coffee
# Pathology = require "pathology"

# One ugly-ish thing about pathology.
# You've got to explicitly name your top-level
# namespaces. Unlike Ember, Pathology isn't going
# to go rooting around the global namespace looking
# for your objects.
Root = Pathology.Namespace.create("Root")

# However, Pathology doesn't need you to explicitly name
# your further nested namespaces. As long as all your objects
# are connected to a root level Namespace.
# You can even reference your objects elsewhere for convenience.
Subspace = Root.Subspace = Pathology.Namespace.create()

# Pathology provides a basic Object class with def/defs/include/extend/create/open semantics.
# When you extend a pathology class you pass in 
Subspace.Thing = Pathology.Object.extend ({def}) ->
  def initialize: (@properties={}) ->

something = Subspace.Thing.create(property: "value")

# And this all comes together for the final awesome-sauce.
puts Subspace.Thing.toString()
# => Root.Subspace.Thing
puts something.toString()
# => <Root.Subspace.Thing:p-9>
puts something.noSuchMethod()
# => TypeError: Object <Root.Subspace.Thing:p-9> has no method 'notAMethod'
```

Stack trace pulled from Taxi, an event library built on Pathology:
(slightly doctored for visual appeal to show off toString goodness)

```
Error: Rejoice in your debugging! :D
  at                                          (taxi/lib/taxi.coffee:88:17)
  at     Array.map                            (native)
  at                                          (taxi/node_modules/underscore/underscore.js:95:56)
  at     Taxi.Path.readToSegment              (taxi/lib/taxi.coffee:86:35)
  at     Taxi.Segment.readSourceProperties    (taxi/lib/taxi.coffee:140:48)
  at     Taxi.Segment.rebind                  (taxi/lib/taxi.coffee:117:12)
  at     Taxi.Path.addSegment                 (taxi/lib/taxi.coffee:69:16)
  at     NS.Evented.bindPath                  (taxi/lib/taxi.coffee:173:17)
  at     Object.<anonymous>                   (taxi/test/taxi.coffee:140:21)
  at     Object.<anonymous>                   (nodeunit/lib/core.js:233:16)
```

### Inheritable Attributes
This one is inspired by an old feature of Rails.

Sometimes you're making a configuration on a class that you want to propagate
to all its subclasses. And you also want to extend that configuration in
the subclasses. An example might be a list of attributes. With ineritable attributes
you might implement this:

```coffee
Model = Root.Model = Pathology.Object.extend()
Model.pushInheritableItem "fields", "id"
Model.pushInheritableItem "fields", "name"

Describable = Model.Describable = Model.extend()
Describable.pushInheritableItem "fields", "description"

puts Model.fields
# => ["id", "name"]
puts Describable.fields
# => ["id", "name", "description"]
```

This can be handy.

There are three methods for dealing with inheritable attributes:

* writeInheritableAttr(slot, value)
* writeInheritableValue(slot, key, value)
* pushInheritableItem(slot, item)

Use them like so:

```coffee
Model.writeInheritableAttr("slot", "value")
puts Describable.slot
# => "value"
Model.writeInheritableValue("family", "name", "Stanislov")
puts Describable.family.name
# => "Stasilov"
Model.pushInheritableItem("fields", "extras")
puts Describable.fields
# => ["id", "name", "description", "extras"]
```

With writeInheritableValue, and pushInheritableitem, when you add a value to a slot
on a child class, the value does not appear on the parent.

### Properties

Pathology allows you to create properties on your objects.

Properties are defined on a constructor. Under the hood properties
are stored in an Inheritable Attribute, so ancestors will have the
properties of their parents.

```coffee
Root.Subspace.Thing.property('aProperty')
```

And property configurations are reflected.

```coffee
Root.Subspace.Thing.properties.aProperty
# => <Pathology.Property:p-23>
```

Property instances are created after the initialize method.

```coffee
Root.Subspace.Thing.create().aProperty
# => <Pathology.Property.Instance:p-24>
```

##### propertiesThatCouldBe
A Pathology object has a special accessor for properties: 'propertiesThatCouldBe'
This iterates over all properties of the object and tests their 'couldBe' method
against a test value. For the vanilla Pathology.Property this is just a 
check against the property name.


##### Extending Pathology.Property
Pathology.Property follows a simple interface. The core property implementation
is the following:

```coffee
Property = Pathology.Object.extend ({def}) ->
  def initialize: (@name, @_constructor) ->
    @_constructor.writeInheritableValue 'properties', @name, this

  def couldBe: (test) ->
    return true if test is @name
    false

  def instance: -> @Instance.create()

Property.Instance = Pathology.Object.extend ({def}) ->
  def get: -> @value

  def set: (value) -> @value = value


```

If you extend Pathology.Property you will most likely be working on the
Instance. See this example from Taxi, an event binding library,

```coffee
Taxi.Property = Pathology.Property.extend()

Taxi.Property.Instance = Pathology.Property.Instance.extend ({def}) ->
  def set: (value) ->
    return value if value is @value
    @value = value
    @trigger "change"
    value
```

### Modules
Pathology includes a simple module system.

Use it more or less like this:

```coffee
Root.Mixable = Pathology.Object.extend()
Root.Mixer = Pathology.Mixin.extend ({def, defs}) ->
  defs staticKey: "Static Value"

  def included: ->
    puts "included in Root.Mixable? ", this is Root.Mixable

  def instance:
    instanceKey: "Instance Value"

Root.Mixable.include(Root.Mixer)
# "included in Root.Mixable? true"
Root.Mixable.staticKey
# => "Static Value"
Root.Mixable.create().instanceKey
# => "Instance Value"
Root.Mixable.isA(Root.Mixer)
# => true
```

### Delegation
Pathology Objects may define method and property delegation.
Defining delegates looks like this:

```coffee
Awesome = Root.Awesome = Pathology.Object.extend ({def, delegate}) ->
  delegate "foo", "bar", to: "target"
  delegate "biz", "baz", to: "other"

  def target:
    foo: "foo"
    bar: -> "bar"

  def other: ->
    return biz: "biz", baz: -> "baz"
```

With this delegation setup we can call the following methods on an Awesom object.
Pathology delegates will inspect the objects that are in the delegation chain
and call methods if they are in the chain.

Here are some calls to delegate methods and the equivalant manual access.

```coffee
awesome = Awesome.create()
# awesome.foo() is the same as awesome.target.foo
awesome.foo()
# => "foo"
# awesome.bar() is the same as awesome.target.bar()
awesome.bar()
# => "bar"
# awesome.biz() is the same as awesome.other().biz
awesome.biz()
# => "biz"
# awesome.baz() is the same as awesome.other().baz()
awesome.baz()
# => "baz"
```

# Changelog

## 0.2.0
* mixin system
* added a delegate mixin
* inheritable attributes
* pathology properties
* turned constructors into Functions, like god intended
* extend classes with a function
* define properties with def/defs
* include with include
* delegate with delegate

## 0.1.0
* basic object system


